         MACRO
.*--------------------------------------------------------------------
.* Name       : MXIGOPTS
.*
.* Function   : Constructs the MXI global options table
.*
.*
.* Syntax
.*
.*            : MXIGOPTS DSCB=(ON/OFF),
.*                    MENUHIDE=(ON/OFF),
.*                    EMCSWAIT=nn,
.*                    EMCSFILL=nn,
.*                    DAENQ=(ON/OFF),
.*                    XMEM=(YES/NO),
.*                    MAXROWS=nnnnn,
.*                    WTORZAP=(ON/OFF),
.*                    MSGMAKE=(ON/OFF),
.*                    MSGOPER=(ON/OFF),
.*                    LOGRRECS=nnnn,
.*                    LOGRRLEN=280,
.*                    LLAMODS=nnnn,
.*                    LLASMF=(NO/nnn),
.*                    CA1PWD=(ON/OFF),
.*                    MQ=cccc,
.*                    DB=cccc,
.*                    DB2MON1=(YES/NO),
.*                    DEFMQQ=queuemask,
.*                    PORT=nnnnn
.*
.* Keywords   : DSCB=(ON/OFF)
.*                 Specifies the global default setting for DSCB
.*                 collection. This setting affects the APF, LINK and
.*                 LPA commands.
.*                 The default setting is OFF.
.*
.*            : MENUHIDE=(ON/OFF)
.*                 Specifies whether commands that the user is not
.*                 authorized to use appear on the primary option
.*                 menu.
.*                 The default setting is ON.
.*
.*            : DAENQ=(ON/OFF)
.*                 Specifies whether the enqueue display is shown
.*                 automatically when the DA command is used on a
.*                 single address space.
.*                 The default setting is ON.
.*
.*            : EMCSWAIT=nn
.*                 Specifies the maximum number of seconds to wait for
.*                 a response to an operator command ('/').
.*                 The default setting is 5 seconds.
.*
.*            : EMCSFILL=nn
.*                 Specifies the number of seconds to wait for the
.*                 EMCS message buffer to fill after the expiration
.*                 of the EMCSWAIT value. A value higher than 1 will
.*                 improve the chance of capturing all of the output
.*                 from commands that produce a high volume of
.*                 messages at the cost of a slower response time.
.*                 The default setting is 1 second.
.*
.*            : TIMEOUT=mmss
.*                 Specifies the timeout value (mm = mins, ss = secs)
.*                 for remote requests. All four digits MUST be
.*                 specified including leading zeros.
.*                 The default setting is 60 seconds = 0060.
.*
.*            : MAXROWS=nnnnn
.*                 Specifies the maximum number of lines that can be
.*                 displayed in response to a single MXI command.
.*                 Any value lower than 1000 or higher than 99999 is
.*                 ignored.
.*                 The default setting is 40000.
.*
.*            : LLAMODS=nnnn
.*                 Specifies the maximum number of LLA modules that
.*                 are tracked via the MXILLIX1 exit.
.*                 Any value lower than 100 or higher than 9999 is
.*                 ignored.
.*                 The default setting is 5000.
.*
.*            : WTORZAP=(ON/OFF)
.*                 Specifies whether a conformation WTOR is issued
.*                 when the ZAP command is used.
.*                 The default setting is ON.
.*
.*            : MSGMAKE=(ON/OFF)
.*                 Specifies whether a audit message is issued when a
.*                 user issues the 'MAKE' command.
.*                 The default setting is OFF.
.*
.*            : MSGOPER=(ON/OFF)
.*                 Specifies whether a audit message is issued when a
.*                 user issues the '/' command.
.*                 The default setting is OFF.
.*
.*            : PORT=nnnnn
.*                 Specifies the port number used by the MXI TCP/IP
.*                 server. This value must be common to all systems
.*                 that run the MXI TCP/IP server and must not be
.*                 shared with any other software.
.*                 The default value is 8001.
.*
.*            : LLASMF=(NO/nnn)
.*                 Specifies the SMF record number to be used to record
.*                 the LLA modules statistics in CSVLLIX1. The SMF
.*                 record appends the LLP1 parameter list to a standard
.*                 SMF record header.
.*                 If you do not require SMF records to be written,
.*                 then specify 'NO'.
.*                 The default setting is NO.
.*
.*            : LOGRRECS=nnnn
.*                 Specifies the maximum number of log streams that
.*                 are recorded from the SMF 88-1 records captured
.*                 by the MXIU83 exit.
.*                 Any value lower than 100 or higher than 9999 is
.*                 ignored.
.*                 The default setting is 1000.
.*
.*            : LOGRRLEN=280
.*                 Specifies the length of a SMF 88-1 record. This
.*                 option should not be changed unless IBM change
.*                 the length of the SMF record.
.*
.*            : CA1PWD=(ON/OFF)
.*                 Specifies whether commands the CA-1 password is
.*                 shown on the CA1 command display.
.*                 The default setting is ON.
.*
.*            : DB=cccc
.*                 Specifies the default DB2 subsystem name to
.*                 use if not specified.
.*                 The default setting is DSN1.
.*
.*            : DB2MON1=(YES/NO)
.*                 Specifies whether MXI will automatically start
.*                 the DB2 MONITOR1 trace when using DBxx commands.
.*                 The default setting is YES.
.*
.*            : MQ=cccc
.*                 Specifies the default MQSeries subsystem name to
.*                 use if not specified.
.*                 The default setting is CSQ1.
.*
.*            : DEFMQQ=queuemask
.*                 Specifies the default queue name mask passed on the
.*                 MQQ.
.*                 The default setting is *
.*
.*            : XMEM=(YES/NO)
.*                 Specifies whether MXI can perform cross-memory
.*                 services for certain authorised commands. This
.*                 includes scheduling SRBs into another address
.*                 space or using AR Mode methods.
.*                 This setting only applies when MXI is running
.*                 in authorised mode.
.*                 The default setting is YES
.*
.*
.*------------------------------------------------------------------*
.* Changes
.* 09/05/2002   Rob Scott     Code Written                     V4.1
.* 17/10/2002   Rob Scott     Added MQ                         V4.2
.*              Rob Scott     Added LOGRRECS                   V4.2
.*              Rob Scott     Added LOGRRLEN                   V4.2
.*              Rob Scott     Added LLASMF                     V4.2
.*              Rob Scott     Added CA1PWD                     V4.2
.*              Rob Scott     Added MQ                         V4.2
.* 19/11/2002   Rob Scott     Added DB                         V4.3
.* 20/01/2003   Rob Scott     Added DEFMQQ                     V4.3
.* 14/02/2003   Rob Scott     Added XMEM                       V4.3
.* 11/03/2003   Rob Scott     Added DB2MON1                    V4.3
.*------------------------------------------------------------------*
.*--------------------------------------------------------------------
&LABEL    MXIGOPTS &DSCB=OFF,                                          X
               &MENUHIDE=ON,                                           X
               &DAENQ=ON,                                              X
               &EMCSWAIT=05,                                           X
               &EMCSFILL=01,                                           X
               &MAXROWS=40000,                                         X
               &LLAMODS=5000,                                          X
               &LOGRRECS=1000,                                         X
               &LOGRRLEN=280,                                          X
               &TIMEOUT=0060,                                          X
               &WTORZAP=ON,                                            X
               &MSGMAKE=OFF,                                           X
               &MSGOPER=OFF,                                           X
               &LLASMF=NO,                                             X
               &CA1PWD=ON,                                             X
               &MQ=CSQ1,                                               X
               &DB=DSN1,                                               X
               &DEFMQQ=*,                                              X
               &XMEM=YES,                                              X
               &DB2MON1=YES,                                           X
               &PORT=8001
MXIOPTN  CSECT
MXIOPTN  AMODE 31
MXIOPTN  RMODE ANY
.DSCBTEST  ANOP
           AIF ('&DSCB' EQ 'ON').DSCBON
           AIF ('&DSCB' EQ 'OFF').DSCBOFF
           AGO  .MSG01
.DSCBON    ANOP
DSCB     DC    X'01'   Set DSCB to ON
           AGO  .MENUTEST
.DSCBOFF   ANOP
DSCB     DC    X'00'   Set DSCB to OFF
           AGO  .MENUTEST
.MENUTEST  ANOP
           AIF ('&MENUHIDE' EQ 'ON').MENUON
           AIF ('&MENUHIDE' EQ 'OFF').MENUOFF
           AGO  .MSG02
.MENUON    ANOP
MENUHIDE DC    X'01'   Hide unauth commands on primary menu
           AGO  .WAITTEST
.MENUOFF   ANOP
MENUHIDE DC    X'00'   Show unauth commands on primary menu
           AGO  .WAITTEST
.WAITTEST  ANOP
           AIF ('&EMCSWAIT' EQ '').MSG03
EMCSWAIT DC    AL1(&EMCSWAIT)
.FILLTEST  ANOP
           AIF ('&EMCSFILL' EQ '').MSG04
EMCSFILL DC    AL1(&EMCSFILL)
.ZAPTEST   ANOP
           AIF ('&WTORZAP' EQ 'ON').ZAPON
           AIF ('&WTORZAP' EQ 'OFF').ZAPOFF
           AGO  .MSG05
.ZAPON     ANOP
WTORZAP  DC    X'01'   Issue WTOR for ZAP commands
           AGO  .MAKETEST
.ZAPOFF    ANOP
WTORZAP  DC    X'00'   Do not issue WTOR for ZAP commands
           AGO  .MAKETEST
.MAKETEST  ANOP
           AIF ('&MSGMAKE' EQ 'ON').MAKEON
           AIF ('&MSGMAKE' EQ 'OFF').MAKEOFF
           AGO  .MSG06
.MAKEON    ANOP
MSGMAKE  DC    X'01'   Issue message when MAKE command entered
           AGO  .OPERTEST
.MAKEOFF   ANOP
MSGMAKE  DC    X'00'   Do not issue message when MAKE command entered
           AGO  .OPERTEST
.OPERTEST  ANOP
           AIF ('&MSGOPER' EQ 'ON').OPERON
           AIF ('&MSGOPER' EQ 'OFF').OPEROFF
           AGO  .MSG07
.OPERON    ANOP
MSGOPER  DC    X'01'   Issue message when / command entered
           AGO  .DAENQTST
.OPEROFF   ANOP
MSGOPER  DC    X'00'   Do not issue message when / command entered
           AGO  .DAENQTST
.DAENQTST  ANOP
           AIF ('&DAENQ' EQ 'ON').DAENQON
           AIF ('&DAENQ' EQ 'OFF').DAENQOFF
           AGO  .MSG011
.DAENQON   ANOP
DAENQ    DC    X'01'   Show enqueues on DA display
           AGO  .PORTTEST
.DAENQOFF  ANOP
DAENQ    DC    X'00'   Do not show enqueues on DA display
.PORTTEST  ANOP
           AIF ('&PORT' EQ '').MSG08
PORT     DC    AL2(&PORT)  TCP/IP Port Number
.MROWTEST  ANOP
           AIF ('&MAXROWS' EQ '').MSG09
           AIF ('&MAXROWS' LT '1000').DEFMROW
           AIF ('&MAXROWS' GT '99999').DEFMROW
MAXROWS  DC    AL4(&MAXROWS) Maximum number of rows on display
           AGO  .LLAMTEST
.DEFMROW   ANOP
         MNOTE 4,'MAXROWS invalid - using default of 40000'
MAXROWS  DC    AL4(40000)  Maximum number of rows on display
.LLAMTEST  ANOP
           AIF ('&LLAMODS' EQ '').MSG010
           AIF ('&LLAMODS' LT '100').DEFLLAM
           AIF ('&LLAMODS' GT '9999').DEFLLAM
LLAMODS  DC    AL4(&LLAMODS) Maximum number of LLA modules tracked
           AGO  .TIMETEST
.DEFLLAM   ANOP
         MNOTE 4,'LLAMODS invalid - using default of 5000'
LLAMODS  DC    AL4(5000) Maximum number of LLA modules tracked
.TIMETEST  ANOP
           AIF ('&TIMEOUT' EQ '').MSG012
TIMEOUT  DC    CL4'&TIMEOUT'  Remote command timeout value
.LLATEST   ANOP
           AIF ('&LLASMF' EQ '').MSG013
           AIF ('&LLASMF' NE 'NO').GOTSMF
LLASMF   DC    AL1(0)  Do not cut an SMF record in MXILLIX1
           AGO  .ENDLLA
.GOTSMF    ANOP
           AIF ('&LLASMF' LT '200').MSG016
           AIF ('&LLASMF' GT '255').MSG016
LLASMF   DC    AL1(&LLASMF) SMF record type for MXILLIX1
.ENDLLA    ANOP
.CA1TEST   ANOP
           AIF ('&CA1PWD' EQ '').MSG014
           AIF ('&CA1PWD' EQ 'ON').CA1ON
           AIF ('&CA1PWD' NE 'OFF').MSG015
CA1PWD   DC    X'00'   Do not show the CA-1 password
           AGO  .ENDCA1
.CA1ON     ANOP
CA1PWD   DC    X'01'   Show the CA-1 password
.ENDCA1    ANOP
.LOGRTEST  ANOP
           AIF ('&LOGRRECS' EQ '').MSG017
           AIF ('&LOGRRLEN' EQ '').MSG018
           AIF ('&LOGRRECS' LT '100').DEFLOGR
           AIF ('&LOGRRECS' GT '9999').DEFLOGR
LOGRRECS DC    AL4(&LOGRRECS) Maximum number of LOGR records
LOGRRLEN DC    AL2(&LOGRRLEN) Length of a SMF 88-1 record
           AGO  .ENDLOGR
.DEFLOGR   ANOP
         MNOTE 4,'LOGRRECS invalid - using default of 1000'
LOGRRECS DC    AL4(1000) Maximum number of LOGR records
LOGRRLEN DC    AL2(&LOGRRLEN) Length of a SMF 88-1 record
.ENDLOGR   ANOP
.MQSUB     ANOP
           AIF ('&MQ' EQ '').MSG019
MQ       DC    CL4'&MQ' MQ Series subsystem
.DBSUB     ANOP
           AIF ('&DB' EQ '').MSG020
DB       DC    CL4'&DB' DB2 Subsystem
           AIF ('&DEFMQQ' EQ '').MSG021
&DEFMQQL   SETA K'&DEFMQQ
           AIF (&DEFMQQL GT 48).MSG022
DEFMQQ   DC    AL1(&DEFMQQL),CL48'&DEFMQQ' Default MQQ queue name mask
.XMEMTEST  ANOP
           AIF ('&XMEM' EQ 'YES').XMEMYES
           AIF ('&XMEM' EQ 'NO').XMEMNO
           AGO  .MSG023
.XMEMYES   ANOP
XMEM     DC    X'01'   Set XMEM to YES
           AGO  .XMEMDONE
.XMEMNO    ANOP
XMEM     DC    X'00'   Set XMEM to NO
.XMEMDONE  ANOP
.MON1TEST  ANOP
           AIF ('&DB2MON1' EQ 'YES').MON1YES
           AIF ('&DB2MON1' EQ 'NO').MON1NO
           AGO  .MSG024
.MON1YES   ANOP
MON1     DC    X'01'   Set DB2MON1 to YES
           AGO  .MON1DONE
.MON1NO    ANOP
MON1     DC    X'00'   Set DB2MON1 to NO
.MON1DONE  ANOP
.OPTNEND   ANOP
RESERVED DS    0D
           END
           AGO  .END
.MSG01     MNOTE 12,'DSCB must be ON or OFF'
           AGO .END
.MSG02     MNOTE 12,'MENUHIDE must be ON or OFF'
           AGO .END
.MSG03     MNOTE 12,'EMCSWAIT must be specified'
           AGO .END
.MSG04     MNOTE 12,'EMCSFILL must be specified'
           AGO .END
.MSG05     MNOTE 12,'WTORZAP must be ON or OFF'
           AGO .END
.MSG06     MNOTE 12,'MSGMAKE must be ON or OFF'
           AGO .END
.MSG07     MNOTE 12,'MSGOPER must be ON or OFF'
           AGO .END
.MSG08     MNOTE 12,'PORT must be specified'
           AGO .END
.MSG09     MNOTE 12,'MAXROWS must be specified'
           AGO .END
.MSG010    MNOTE 12,'LLAMODS must be specified'
           AGO .END
.MSG011    MNOTE 12,'DAENQ must be specified'
           AGO .END
.MSG012    MNOTE 12,'TIMEOUT must be specified'
           AGO .END
.MSG013    MNOTE 12,'LLASMF must be specified'
           AGO .END
.MSG014    MNOTE 12,'CA1PWD must be specified'
           AGO .END
.MSG015    MNOTE 12,'CA1PWD must be ON or OFF'
           AGO .END
.MSG016    MNOTE 12,'LLASMF must specify a number between 200 and 255'
           AGO .END
.MSG017    MNOTE 12,'LOGRRECS must be specified'
           AGO .END
.MSG018    MNOTE 12,'LOGRRLEN must be specified'
           AGO .END
.MSG019    MNOTE 12,'MQ must be specified'
           AGO .END
.MSG020    MNOTE 12,'DB must be specified'
           AGO .END
.MSG021    MNOTE 12,'DEFMQQ must be specified'
           AGO .END
.MSG022    MNOTE 12,'DEFMQQ must be between 1 and 48 characters'
           AGO .END
.MSG023    MNOTE 12,'XMEM must be YES or NO'
           AGO .END
.MSG024    MNOTE 12,'DB2MON1 must be YES or NO'
           AGO .END
.END     MEND
