         MACRO
.*--------------------------------------------------------------------
.* Name       : MXIMENU
.*
.* Function   : Constructs the MXI menu choices
.*
.*
.* Syntax     : MXIMENU START
.*
.*            : MXIMENU END
.*
.*            : MXIMENU SET,
.*                    COMMAND=command,
.*                    GROUP=command-group,
.*                    AUTH=command-auth-check,
.*                    OSLEVEL=opsys-level,
.*                    COMMENT=description
.*
.* Keywords   : START
.*                 Denotes the beginning of the table.
.*                 This must be the first entry.
.*
.*            : END
.*                 Denotes the end of the table.
.*
.*            : SET
.*                 Creates a menu choice in the current table.
.*
.*
.*------------------------------------------------------------------*
&LABEL    MXIMENU &INVOKE,                                             X
               &SUFFIX=00,                                             X
               &COMMAND=,                                              X
               &KEYWORDS='',                                           X
               &GROUP=SYSTEM,                                          X
               &AUTH=NO,                                               X
               &COMMENT=
.*--------------------------------------------------------------------
.* Ensure that we have all required parms
.*--------------------------------------------------------------------
           AIF ('&INVOKE' EQ 'START').MENUSTRT
           AIF ('&INVOKE' EQ 'END').MENUEND
           AIF ('&INVOKE' EQ 'SET').MENUSET
           AGO .MSG0001
.MENUSTRT  ANOP
MXIMEN&SUFFIX CSECT
           DC   CL8'MXIMEN&SUFFIX'
           DC   CL8'&SYSDATE'
           DC   CL8'&SYSTIME'
           DC   AL4(NUMMEN&SUFFIX)
           DC   F'0'
BEGMEN&SUFFIX DS   0D
           AGO  .END
.MENUSET   ANOP
.*--------------------------------------------------------------------
.* Process the defined choices
.*--------------------------------------------------------------------
           AIF  ('&COMMAND' EQ '').MSG0002
           AIF  ('&GROUP' EQ '').MSG0003
           AIF  ('&AUTH' EQ '').MSG0004
&CMDL      SETA K'&COMMAND
           AIF  (&CMDL GT 4).MSG0005
&KEYL      SETA K'&KEYWORDS-2
           AIF  (&KEYL GT 15).MSG0006
&GRPL      SETA K'&GROUP
           AIF  (&GRPL GT 8).MSG0007
&COML      SETA K'&COMMENT-2
           AIF  (&COML GT 32).MSG0008
           DC   AL1(&CMDL)
           DC   CL4'&COMMAND'      Command
           DC   AL1(&KEYL)
           DC   CL15&KEYWORDS      Command
           DC   AL1(&GRPL)
           DC   CL8'&GROUP'        Command Group
           DC   AL1(&COML)
           DC   CL32&COMMENT       Description
           DC   CL1'&AUTH(1,1)'
           DC   CL16' '            Reserved
           AGO .END
.MENUEND   ANOP
ENDMEN&SUFFIX DS 0H
NUMMEN&SUFFIX EQU (ENDMEN&SUFFIX-BEGMEN&SUFFIX)/80
           END
           AGO  .END
.MSG0001   MNOTE 12,'Invalid Invocation - Use START/SET/END'
           AGO .END
.MSG0002   MNOTE 12,'COMMAND must be specified'
           AGO .END
.MSG0003   MNOTE 12,'GROUP must be specified'
           AGO .END
.MSG0004   MNOTE 12,'AUTH must be specified'
           AGO .END
.MSG0005   MNOTE 12,'COMMAND must be 4 characters or less'
           AGO .END
.MSG0006   MNOTE 12,'KEYWORDS must be 15 characters or less'
           AGO .END
.MSG0007   MNOTE 12,'GROUP must be 8 characters or less'
           AGO .END
.MSG0008   MNOTE 12,'COMMENT must be 32 characters or less'
           AGO .END
.END     MEND
